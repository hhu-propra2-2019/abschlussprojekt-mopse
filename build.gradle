plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '4.0.4'
    id 'io.franzbecker.gradle-lombok' version '3.3.0'
}

group = 'mops'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url = 'https://s3.cs.hhu.de/public/mops/'
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.0.0'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'org.postgresql:postgresql'

    testRuntimeOnly 'com.h2database:h2'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'mops:styleguide:2.2.0'
    implementation 'io.minio:minio:6.0.13'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.3.6'
    implementation 'org.keycloak:keycloak-spring-boot-starter:9.0.0'
    implementation 'org.keycloak.bom:keycloak-adapter-bom:9.0.0'
    implementation group: 'org.springframework.security.oauth', name: 'spring-security-oauth2', version: '2.4.0.RELEASE'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.c4-soft.springaddons:spring-security-test-keycloack-addons:1.2.0'
    testImplementation 'com.tngtech.archunit:archunit-junit5:0.13.1'
    testImplementation 'org.testcontainers:testcontainers:1.13.0'
    testImplementation 'org.testcontainers:junit-jupiter:1.13.0'
}

checkstyle.toolVersion = '8.30'
checkstyleTest.enabled = false
tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    toolVersion = '6.22.0'
    ruleSets = []
    ruleSetFiles = files 'config/pmd/pmd.xml'
}
pmdTest.enabled = false
tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugs.toolVersion = '4.0.1'
tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file "$buildDir/delombok"
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    destinationDir = file "doc/javadoc"
    failOnError = false
}

test {
    useJUnitPlatform()
}
